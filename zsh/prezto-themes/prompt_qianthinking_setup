#
# A theme based on steeef theme
#  * RVM/Rbenv info shown on the right
#  * Git branch info on the left
#  * Single line prompt
#
# Authors:
#   Leon Li <qianthinking@gmail.com>
#   Steve Losh <steve@stevelosh.com>
#   Bart Trojanowski <bart@jukie.net>
#   Brian Carper <brian@carper.ca>
#   steeef <steeef@gmail.com>
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#   Yan Pritzker <yan@pritzker.ws>

function prompt_qianthinking_precmd {
  current_time=`date "+%H:%M:%S"`
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info on
    git-info
  fi

  # Get ruby information
  if (( $+functions[ruby-info] )); then
    ruby-info
  fi

  # Get node information
  if (( $+functions[node-info] )); then
    node-info
  fi
}

function prompt_qianthinking_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  autoload -Uz add-zsh-hook

  add-zsh-hook precmd prompt_qianthinking_precmd

  # Use extended color pallete if available.
  if [[ $TERM = *256color* || $TERM = *rxvt* ]]; then
    __PROMPT_QIANTHINKING_COLORS=(
      "%F{74}"  # turquoise
      "%F{130}" # orange
      "%F{97}" # purple
      "%F{161}" # hotpink
      "%F{70}" # limegreen
    )
  else
    __PROMPT_QIANTHINKING_COLORS=(
      "%F{cyan}"
      "%F{yellow}"
      "%F{magenta}"
      "%F{red}"
      "%F{green}"
    )
  fi

  # git
  #zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:branch' format "${__PROMPT_QIANTHINKING_COLORS[1]}%b%f"
  #zstyle ':prezto:module:git:info:remote' format 'remote:%R'
  zstyle ':prezto:module:git:info:added' format "${__PROMPT_QIANTHINKING_COLORS[5]}●%f"
  zstyle ':prezto:module:git:info:deleted' format "${__PROMPT_QIANTHINKING_COLORS[2]}●%f"
  zstyle ':prezto:module:git:info:modified' format "${__PROMPT_QIANTHINKING_COLORS[4]}●%f"
  zstyle ':prezto:module:git:info:untracked' format "${__PROMPT_QIANTHINKING_COLORS[3]}●%f"
  zstyle ':prezto:module:git:info:keys' format 'prompt' '(%b%d%a%m%u%c)'

  # ruby info (rvm, rbenv)
  zstyle ':prezto:module:ruby:info:version' format '[%v]'
  # node info (nvm)
  zstyle ':prezto:module:node:info:version' format '[%v]'
  NEWLINE=$'\n»%f '
  PROMPT="${__PROMPT_QIANTHINKING_COLORS[3]}%n%f@${__PROMPT_QIANTHINKING_COLORS[2]}%m%f ${__PROMPT_QIANTHINKING_COLORS[5]}%~%f "'$git_info[prompt]'"%(?.%F{norm}${__PROMPT_QIANTHINKING_COLORS[5]}${NEWLINE}.%F{red}(%?%)%F{norm}${__PROMPT_QIANTHINKING_COLORS[4]}${NEWLINE}"
  RPROMPT='%F{24}${node_info[version]}${ruby_info[version]}[$current_time]'
}

function prompt_awreece_is_ssh {
  # Actually, sudo etc clear a bunch of environment variables, including
  # SSH_CONNECTION, so this doesn't always work. Unfortunately, I don't know
  # the best way to make it work. For now, I'll hide it in a function and I can
  # update it when I figure out a better way later.
  [[ -n $SSH_CONNECTION ]]
}
prompt_qianthinking_setup "$@"
